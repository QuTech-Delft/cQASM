# Implementation for the various classes representing the types of values
# available in cQASM.
source

# Header file for the various classes representing the types of values
# available in cQASM.
header "v1x/cqasm-types-gen.hpp"

// Include tree base classes.
include "cqasm-tree.hpp"
tree_namespace cqasm::tree

// Include primitive types.
include "v1x/cqasm-primitives.hpp"
import cqasm.v1x.primitives


// Initialization function to use to construct default values for the tree base
// classes and primitives.
initialize_function cqasm::v1x::primitives::initialize
serdes_functions cqasm::v1x::primitives::serialize cqasm::v1x::primitives::deserialize

# Namespace for the various classes representing the types of values available
# in cQASM.
namespace cqasm
namespace v1x
namespace types

# Base for all types, with a member indicating whether values of this type are
# assignable (references) or not (constants, expression results).
type_base {

    # Whether a value of this type can be assigned (true), or can only be read
    # (false). For constraints, whether the value must be assignable (true), or
    # only has to be read. In C++ terminology, true = lvalue, false = rvalue
    # (more or less).
    assignable: cqasm::v1x::primitives::Bool;

    # Type of one or more qubit references.
    qubit {}

    # Type of a boolean/bit.
    bool {}

    # Type of an axis (x, y, or z).
    axis {}

    # Type of an integer (signed 64-bit).
    int {}

    # Type of a real number (IEEE double).
    real {}

    # Type of a complex number (2x IEEE double).
    complex {}

    # Type of a real matrix. Also used for one-dimensional arrays and vectors.
    real_matrix {

        # Number of rows. Negative indicates that the value is unconstrained.
        # Must be one or more otherwise.
        num_rows: cqasm::v1x::primitives::Int;

        # Number of columns. Negative indicates that the value is
        # unconstrained. Can be zero, unlike num_rows.
        num_cols: cqasm::v1x::primitives::Int;

    }

    # Type of a complex matrix. Also used for one-dimensional arrays and
    # vectors.
    complex_matrix {

        # Number of rows. Negative indicates that the value is unconstrained.
        # Must be one or more otherwise.
        num_rows: cqasm::v1x::primitives::Int;

        # Number of columns. Negative indicates that the value is
        # unconstrained. Can be zero, unlike num_rows.
        num_cols: cqasm::v1x::primitives::Int;

    }

    # Type of an arbitrary string.
    string {}

    # Type of a JSON string.
    json {}

}
